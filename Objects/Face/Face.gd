## Set of sprites that identify a unique face.
@tool
class_name Face
extends Node2D


############### Exported variables

## Current base face shape variant
@export_range(1, 3) var base_variant := 1:
	set(new_val):
		base_variant = new_val
		_sync_face.call_deferred()
## Current eyes variant
@export_range(1, 3) var eyes_variant := 1:
	set(new_val):
		eyes_variant = new_val
		_sync_face.call_deferred()
## Current hair variant
@export_range(1, 3) var hair_variant := 1:
	set(new_val):
		hair_variant = new_val
		_sync_face.call_deferred()
## Current skin color (must be between 1 and skin_colors.size(), inclusive)
@export var skin_color := 1:
	set(new_val):
		skin_color = new_val
		_sync_face.call_deferred()
## Current hair color (must be between 1 and hair_colors.size(), inclusive)
@export var hair_color := 1:
	set(new_val):
		hair_color = new_val
		_sync_face.call_deferred()
## Possible skin colors
@export var skin_colors : Array[Color]
## Possible hair colors
@export var hair_colors : Array[Color]
## Whether the mask is currently placed on the face.
@export var mask_visible := false:
	set(new_val):
		mask_visible = new_val
		_sync_face.call_deferred()

## Test the randomization logic in-editor.
@export_tool_button("randomize", "Callable") var randomize_action = _randomize

@export_group("Node References")
@export var base_sprite: Sprite2D
@export var eyes_sprite: Sprite2D
@export var hairs_sprite: Sprite2D
@export var mask_sprite: Sprite2D
@export var mask_animation_player: AnimationPlayer
@export var large_rip_player: AudioStreamPlayer
@export var scream_player: AudioStreamPlayer


############### Private variables

var _cur_face_randomizer_result: FaceRandomizer.FaceRandomizerResult = null



# ================================= Public ================================== #


## Applies a result generated by the FaceRandomizer node to this face.
func apply_face_randomizer_result(result: FaceRandomizer.FaceRandomizerResult) -> void:
	base_variant = result.face
	eyes_variant = result.eyes
	hair_variant = result.hair
	skin_color = result.skin_color
	hair_color = result.hair_color
	_cur_face_randomizer_result = result


## True if the current face matches the provided FaceRandomizerResult.
func matches_result(result: FaceRandomizer.FaceRandomizerResult) -> bool:
	if _cur_face_randomizer_result == null:
		return false
	return result.is_same(_cur_face_randomizer_result)


# ================================= Private ================================= #


## Resets the face back to the "no mask" state
func reset_mask() -> void:
	mask_animation_player.play("default")


## Rips off the mask
func yank_mask_off() -> void:
	mask_animation_player.play("yank_off")


## Rips of the mask for the "chosen one".
## Since no animation was made for this, it will only play a different sound.
func yank_mask_off_hard() -> void:
	large_rip_player.play()
	scream_player.play()


## Uses the current values to display the appropiate face.
func _sync_face() -> void:

	mask_sprite.visible = mask_visible

	base_sprite.frame = base_variant - 1
	eyes_sprite.frame = eyes_variant - 1
	hairs_sprite.frame = hair_variant - 1

	if skin_color - 1 >= skin_colors.size():
		base_sprite.modulate = Color.WHITE
	else:
		base_sprite.modulate = skin_colors[skin_color - 1]
	
	if hair_color - 1 >= hair_colors.size():
		hairs_sprite.modulate = Color.WHITE
	else:
		hairs_sprite.modulate = hair_colors[hair_color - 1]


## Randomize face values.
func _randomize() -> void:
	base_variant = randi_range(1, 3)
	eyes_variant = randi_range(1, 3)
	hair_variant = randi_range(1, 3)
	skin_color = randi_range(1, skin_colors.size() + 1)
	hair_color = randi_range(1, hair_colors.size() + 1)
